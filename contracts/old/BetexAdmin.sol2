pragma solidity 0.5.10;

import "./BetexSettings.sol";
/**
 * @title BetexAdmin
 * @dev Este contrato permite configurar cuál es el mínimo de apuestas y las comisiones 
 *      que se le cobran al ganador del las apuestas. También permite administrar los 
 *      mercados. 
 */
contract BetexAdmin is BetexSettings {
    //Estados de los mercados
    enum MarketStatus { ACTIVE      //Mercado Activo: Se puede apostar
                      , SUSPENDED   //Mercado suspendido: NO se puede apostar y espera a que finalice un evento
                      , CLOSED  }   //Cerrado: El mercado está cerrado

    event OpenMarket(uint128 marketId);
    event ClosedMarket(uint128 marketId);      
    event SuspendedMarket(uint128 marketId);            


    struct Market {
        MarketStatus marketStatus; //Estado de la apuesta 
    }

    //Mercados de las apuestas
    mapping(uint128 => bool)   internal marketsExists;  
    mapping(uint128 => Market) internal markets; 

    /**
     * @dev Verifica que haya un mínimo stake
     */
    modifier minStake(){
        require(msg.value >= minimumStake, "No llegó a la apuesta mínima");
        _;
    }

    /**
     * @dev El mercado tiene que estar activo
     */
    modifier activeMarket(uint128 _marketId){
         //El mercado tiene que existir
        require(marketsExists[_marketId], "El mercado no existe");

        //El mercado tiene que existir
        require(markets[_marketId].marketStatus == MarketStatus.ACTIVE, "El mercado no está activo");
        _;       
    }

    /**
     * @dev El mercado tiene que estar activo o suspendido
     */
    modifier activeOrSupendedMarket(uint128 _marketId){
         //El mercado tiene que existir
        require(marketsExists[_marketId], "El mercado no existe");

        //El mercado tiene que existir
        require(markets[_marketId].marketStatus == MarketStatus.ACTIVE || 
                markets[_marketId].marketStatus == MarketStatus.SUSPENDED, 
                "El mercado no está activo");
        _;       
    }
    
    /**
     * @dev Agrega un mercado a la base de datos
     * @param _marketIdLaursia Id en Laurasia
     */
    function openMarket(uint128 _marketIdLaursia) public onlyMarketManager(){
        marketsExists[_marketIdLaursia] = true;
        markets[_marketIdLaursia].marketStatus = MarketStatus.ACTIVE;
        emit OpenMarket(_marketIdLaursia);
    }    

    /**
     * @dev Verifica si el mercado existe
     * @param _marketIdLaursia Id en Laurasia
     * @return true si existe, false de lo contrario
     */
    function marketExists(uint128 _marketIdLaursia) public view returns(bool){
        return marketsExists[_marketIdLaursia];
    }

    /**
     * @dev Suspende un mercado
     * @param _marketIdLaursia Id en Laurasia
     */
    function suspendMarket(uint128 _marketIdLaursia) public onlyMarketManager() 
                        activeMarket(_marketIdLaursia){
        markets[_marketIdLaursia].marketStatus = MarketStatus.SUSPENDED;
        emit SuspendedMarket(_marketIdLaursia); 
    }    

    /**
     * @dev Cierra un mercado
     * @param _marketIdLaursia Id en Laurasia
     */
    function closeMarket(uint128 _marketIdLaursia) public onlyMarketManager() 
                        activeOrSupendedMarket(_marketIdLaursia){
        markets[_marketIdLaursia].marketStatus = MarketStatus.CLOSED;   
        emit ClosedMarket(_marketIdLaursia);
    }
}    